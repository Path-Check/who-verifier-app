plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "org.who.ddccverifier"
        minSdk 25
        targetSdk 30
        versionCode 15
        versionName "0.0.15"

        buildConfigField "String", "OPENID_CLIENT_ID", "\"verifier-app\""
        buildConfigField "String", "OPENID_REDIRECT_URI", "\"${applicationId}:/redirect\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [ 'appAuthRedirectScheme': "${applicationId}" ]
    }

    buildTypes {
        debug {
            buildConfigField "String", "OPENID_SERVER_URL", "\"http://${getDevHost()}:8080/auth/realms/master/\""
            buildConfigField "String", "TRUST_REGISTRY_URL", "\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/registry_normalized.csv\""
        }
        release {
            buildConfigField "String", "OPENID_SERVER_URL", "null"
            buildConfigField "String", "TRUST_REGISTRY_URL", "\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/registry_normalized.csv\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        pickFirst("META-INF/ASL-2.0.txt")
        pickFirst("META-INF/LGPL-3.0.txt")
        pickFirst("license.html")
        pickFirst("readme.html")
        pickFirst("META-INF/sun-jaxb.episode")
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE.md'
    }
    sourceSets {
        // Shares Resources directory from test to Instrumented tests
        androidTest {
            resources.srcDirs += ['src/test/resources']
            resources.srcDirs += ['src/main/assets']
        }
        // Copy App asset files to the test resources.
        test {
            resources.srcDirs += ['src/main/assets']
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation "androidx.cardview:cardview:1.0.0"

    // Necessary to open a camera.
    implementation 'androidx.camera:camera-camera2:1.1.0-beta02'
    implementation 'androidx.camera:camera-lifecycle:1.1.0-beta02'
    implementation 'androidx.camera:camera-view:1.1.0-beta02'

    // Necessary to find a QR Code in camera preview frame
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    // Necessary to show the "Verified" Checkmark on the UI
    implementation 'info.androidhive:fontawesome:0.0.5'

    // Necessary to unpack the EU's DDC HCERT QR Codes.
    implementation 'io.github.ehn-digital-green-development:base45:0.0.3'
    implementation 'com.augustcellars.cose:cose-java:1.1.0'

    // Necessary to unpack SHC QR Codesjsonld-common-java
    implementation 'com.nimbusds:nimbus-jose-jwt:9.20'

    // Necessary to unpack DIVOC QR Codes
    // LD-signatures requires Jakarta JSON and HTTP Client
    implementation (group: 'javax.json', name: 'javax.json-api', version: '1.1.4')
    implementation (group: 'org.glassfish', name: 'jakarta.json', version: '2.0.1')

    // using from Libs: Titanium jsonld requires valid DIDs. Since DIVOC uses invalid dids,
    // we are using a modified version that removes the skipping of invalid URIs on JsonLdToRdf
    //implementation (group: 'com.apicatalog', name: 'titanium-json-ld', version: '1.3.0-SNAPSHOT') //https://github.com/decentralized-identity/jsonld-common-java/pull/6

    implementation ('io.setl:rdf-urdna:1.1') { //https://github.com/decentralized-identity/jsonld-common-java/pull/6
        exclude group: 'com.apicatalog', module: 'titanium-json-ld'
    }

    implementation ('info.weboftrust:ld-signatures-java:1.0.0') {
        exclude group: 'net.jcip', module: 'jcip-annotations'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'

        // using from Libs remove when this PR gets approved: https://github.com/decentralized-identity/jsonld-common-java/pull/6
        exclude group: 'com.apicatalog', module: 'titanium-json-ld'
    }

    // Required for ICAO QR Codes
    implementation 'io.github.erdtman:java-json-canonicalization:1.1'

    // Required for the Online update of a Credential
    implementation 'net.openid:appauth:0.11.1'

    // Necessary to Convert the QR into the DDCC FHIR Composite
    implementation 'com.google.android.fhir:engine:0.1.0-alpha06'

    // Use BouncyCastle to not rely on each Android's version hability to unpack and load public keys
    // Android 10+ issues due to the move to Conscrypt on a older verion of BC
    // It's already imported by the LD-Signatures dependencies
    //implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'

    // Necessary to Load Jackson-based FHIR Libraries (FHIR Helper, Model Info, and CQL Rules)
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.13.1'

    // Necessary to call JSON APIs (Download the Trust Registry)
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1'

    // Necessary to Load CQL Engine's evaluation classes generated with JAXB (Versions must match)
    implementation 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'

    // Necessary to Execute CQL rules
    // Replace the lib directory by the following versions when they release
    // The lib folder contains those projects without any JAXB dependency. 
    implementation fileTree(dir: 'libs', include: '**.jar')
    implementation 'org.opencds.cqf.cql:engine:1.5.4'
    implementation 'org.opencds.cqf.cql:engine.fhir:1.5.4'
    implementation ('org.opencds.cqf.cql:evaluator.engine:1.4.2') {
        exclude group: 'org.opencds.cqf.cql', module: 'evaluator.cql2elm' // Do not import the conversion
        exclude group: 'org.opencds.cqf.cql', module: 'evaluator.fhir' // Do not import the conversion
        exclude group: 'info.cqframework', module: 'cql-to-elm'           // Do not import the conversion
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-client'     // Do not import FHIR
    }
    implementation ('org.opencds.cqf.cql:evaluator.fhir:1.4.2') {
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-client'                       // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-structures-dstu3'             // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-validation-resources-dstu3'   // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-structures-r4'                // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-validation-resources-r4'      // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-structures-r5'                // Do not import FHIR
        exclude group: 'ca.uhn.hapi.fhir', module: 'hapi-fhir-validation-resources-r5'      // Do not import FHIR
        exclude group: 'javax.inject', module: 'javax.inject'                               // Do not import FHIR
        exclude group: 'org.fhir', module: 'ucum'
    }

    // Tests outside Android can process CQL.
    // Good to test new rules without having to convert to JSON
    testImplementation 'junit:junit:4.13.2'
    testImplementation group: 'info.cqframework', name: 'cql-to-elm', version: '1.5.6'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // ICAO Masterlist import test requires CMSSignedData which is available in this package
    androidTestImplementation 'org.bouncycastle:bcpkix-jdk15to18:1.68'
}

/**
 * Find the first LAN address of the development machine.
 */
def getDevHost() {
    def defaultDevHost = InetAddress.getLocalHost().getCanonicalHostName()

    return NetworkInterface.getNetworkInterfaces().findResult(defaultDevHost) {
        def ifOk = it.isUp() &&
                !it.isVirtual() &&
                !it.isPointToPoint() &&
                !it.isLoopback() &&
                !it.getName().startsWith("br-") //

        if (ifOk)
            return it.getInetAddresses().find {
                it instanceof Inet4Address && !it.isLoopbackAddress()
            }
        else
            return null
    }
}