plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "org.who.ddccverifier"
        minSdk 26
        targetSdk 33
        versionCode 21
        versionName "0.0.21"

        buildConfigField "String", "OPENID_CLIENT_ID", "\"verifier-app\""
        buildConfigField "String", "OPENID_REDIRECT_URI", "\"${applicationId}:/redirect\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [ 'appAuthRedirectScheme': "${applicationId}" ]
    }

    buildTypes {
        debug {
            buildConfigField "String", "OPENID_SERVER_URL", "\"http://${getDevHost()}:8080/auth/realms/master/\""
            buildConfigField "org.who.ddccverifier.trust.TrustRegistry.RegistryEntity[]", "TRUST_REGISTRY_URL",
                    "{ " +
                            "new org.who.ddccverifier.trust.TrustRegistry.RegistryEntity(" +
                                    "org.who.ddccverifier.trust.TrustRegistry.Scope.PRODUCTION, " +
                                    "java.net.URI.create(\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/registry_normalized.csv\"), " +
                                    "null" +
                            "), "+
                            "new org.who.ddccverifier.trust.TrustRegistry.RegistryEntity(" +
                                    "org.who.ddccverifier.trust.TrustRegistry.Scope.ACCEPTANCE_TEST, " +
                                    "java.net.URI.create(\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/test_registry_normalized.csv\")" +
                                    ", null" +
                            ")"+
                    "}"
        }
        release {
            buildConfigField "String", "OPENID_SERVER_URL", "null"
            buildConfigField "org.who.ddccverifier.trust.TrustRegistry.RegistryEntity[]", "TRUST_REGISTRY_URL",
                    "{ " +
                            "new org.who.ddccverifier.trust.TrustRegistry.RegistryEntity(" +
                                    "org.who.ddccverifier.trust.TrustRegistry.Scope.PRODUCTION, " +
                                    "java.net.URI.create(\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/registry_normalized.csv\"), " +
                                    "null" +
                            "), "+
                            "new org.who.ddccverifier.trust.TrustRegistry.RegistryEntity(" +
                                    "org.who.ddccverifier.trust.TrustRegistry.Scope.ACCEPTANCE_TEST, " +
                                    "java.net.URI.create(\"https://raw.githubusercontent.com/Path-Check/trust-registry/main/test_registry_normalized.csv\")" +
                                    ", null" +
                            ")"+
                    "}"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE.md', 'META-INF/NOTICE.md']
            pickFirsts += ['META-INF/ASL-2.0.txt', 'META-INF/LGPL-3.0.txt', 'license.html', 'readme.html', 'META-INF/sun-jaxb.episode']
        }
    }
    sourceSets {
        // Shares Resources directory from test to Instrumented tests
        androidTest {
            resources.srcDirs += ['src/main/assets']
        }
        // Copy App asset files to the test resources.
        test {
            resources.srcDirs += ['src/main/assets']
        }
    }
    namespace 'org.who.ddccverifier'
}

configurations {
    implementation {
        exclude module: "xpp3"
        exclude module: "xpp3_min"
        exclude module: "xmlpull"
        exclude module: "javax.json"
        exclude module: "jcl-over-slf4j"
        exclude group: "org.apache.httpcomponents"
        exclude module: "antlr4"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation "androidx.cardview:cardview:1.0.0"

    // Necessary to open a camera.
    implementation 'androidx.camera:camera-camera2:1.2.0-rc01'
    implementation 'androidx.camera:camera-lifecycle:1.2.0-rc01'
    implementation 'androidx.camera:camera-view:1.2.0-rc01'

    implementation project(':verify')
    implementation project(":trust")
    implementation project(":trust-pathcheck")

    // Necessary to unpack DIVOC QR Codes
    // LD-signatures requires Jakarta JSON and HTTP Client
    implementation (group: 'org.glassfish', name: 'jakarta.json', version: '2.0.1')

    // Required for the Online update of a Credential
    implementation 'net.openid:appauth:0.11.1'

    // Necessary to find a QR Code in camera preview frame
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    // Necessary to show the "Verified" Checkmark on the UI
    implementation 'info.androidhive:fontawesome:0.0.5'

    // Necessary to Load Jackson-based FHIR Libraries (FHIR Helper, Model Info, and CQL Rules)
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.13.3'

    // Necessary to call JSON APIs (Download the Trust Registry)
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'

    // Necessary to Load CQL Engine's evaluation classes generated with JAXB (Versions must match)
    implementation 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'

    // FHIR Engine
    implementation 'com.google.android.fhir:engine:0.1.0-beta02'
    // CQL Engine
    implementation 'com.google.android.fhir:workflow:0.1.0-alpha02'

    // Necessary to Execute CQL rules
    implementation 'org.opencds.cqf.cql:engine:2.1.0'
    implementation 'org.opencds.cqf.cql:engine.jackson:2.1.0'
    implementation 'org.opencds.cqf.cql:engine.fhir:2.1.0'
    implementation 'org.opencds.cqf.cql:evaluator.fhir:2.1.0'
    implementation 'org.opencds.cqf.cql:evaluator.builder:2.1.0'
    implementation 'org.opencds.cqf.cql:evaluator.plandefinition:2.1.0'
    implementation 'org.opencds.cqf.cql:evaluator.measure:2.1.0'
    implementation 'org.opencds.cqf.cql:evaluator.measure-hapi:2.1.0'

    // Tests outside Android can process CQL.
    // Good to test new rules without having to convert to JSON
    testImplementation group: 'info.cqframework', name: 'cql-to-elm', version: '2.2.0'
    testImplementation 'androidx.test:core-ktx:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.8'
    testImplementation project(':test-resources')

    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation project(':test-resources')
}

/**
 * Find the first LAN address of the development machine.
 */
static def getDevHost() {
    def defaultDevHost = InetAddress.getLocalHost().getCanonicalHostName()

    return NetworkInterface.getNetworkInterfaces().findResult(defaultDevHost) {
        def ifOk = it.isUp() &&
                !it.isVirtual() &&
                !it.isPointToPoint() &&
                !it.isLoopback() &&
                !it.getName().startsWith("br-") //

        if (ifOk)
            return it.getInetAddresses().find {
                it instanceof Inet4Address && !it.isLoopbackAddress()
            }
        else
            return null
    }
}