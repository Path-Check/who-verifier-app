plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "org.who.ddccverifier"
        minSdk 25
        targetSdk 30
        versionCode 3
        versionName "0.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations {
        all*.exclude group: 'xml-apis'  // Duplicated class package with Eclipse Moxy
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        pickFirst("META-INF/ASL-2.0.txt")
        pickFirst("META-INF/LGPL-3.0.txt")
        pickFirst("license.html")
        pickFirst("readme.html")
        pickFirst("META-INF/sun-jaxb.episode")
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/NOTICE.md'
    }
    sourceSets {
        // Shares Resources directory from test to Instrumented tests
        androidTest {
            resources.srcDirs += ['src/test/resources']
            resources.srcDirs += ['src/main/assets']
        }
        // Copy App asset files to the test resources.
        test {
            resources.srcDirs += ['src/main/assets']
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // Necessary to open a camera.
    implementation 'androidx.camera:camera-camera2:1.0.2'
    implementation 'androidx.camera:camera-lifecycle:1.0.2'
    implementation 'androidx.camera:camera-view:1.0.0-alpha31'

    // Necessary to find a QR Code in camera preview frame
    implementation 'com.google.mlkit:barcode-scanning:17.0.0'

    // Necessary to show the "Verified" Checkmark on the UI
    implementation 'info.androidhive:fontawesome:0.0.5'

    // Necessary to unpack the EU's DDC HCERT QR Codes.
    implementation 'io.github.ehn-digital-green-development:base45:0.0.3'
    implementation 'com.augustcellars.cose:cose-java:1.1.0'

    // Necessary to Convert the QR into the DDCC FHIR Composite
    implementation 'com.google.android.fhir:engine:0.1.0-alpha05'

    // Necessary to Load Jackson-based FHIR Libraries (FHIR Helper, Model Info, and CQL Rules)
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.13.0'

    // Necessary to Load CQL Engine's evaluation classes generated with JAXB (Versions must match)
    implementation 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'

    // Necessary to Execute CQL rules
    // Replace the lib directory by the following versions when they release
    // The lib folder contains those projects without any JAXB dependency. 
    implementation fileTree(dir: 'libs', include: '**.jar')
    //implementation 'org.opencds.cqf.cql:engine:1.5.3'
    //implementation 'org.opencds.cqf.cql:engine.fhir:1.5.3'
    //implementation 'org.opencds.cqf.cql:evaluator.engine:1.3.1'
    //implementation 'org.opencds.cqf.cql:evaluator.engine.fhir:1.3.1'

    // Tests outside Android can process CQL.
    // Good to test new rules without having to convert to JSON
    testImplementation 'junit:junit:4.13.2'
    testImplementation group: 'info.cqframework', name: 'cql-to-elm', version: '1.5.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}